/**
 * Copyright [2016] <jacek.marchwicki@gmail.com>
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.model.library'
apply plugin: 'com.github.dcendents.android-maven'


group = 'com.appunite'
archivesBaseName = 'lib-leveldb-jni'
version = '0.0.5'

ext {
    appDescription = "Is very fast easy to use key-value database for Android"
    appWebsiteUrl = "https://github.com/jacek-marchwicki/leveldb-jni"
    appGirUrl = 'https://github.com/jacek-marchwicki/leveldb-jni.git'
}

model {

    android {
        compileSdkVersion = 23
        buildToolsVersion = '23.0.2'

        defaultConfig.with {
            minSdkVersion.apiLevel = 8
            targetSdkVersion.apiLevel = 23
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }

        compileOptions.with {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }

        ndk {
            moduleName = 'leveldb-jni'
            stl = 'gnustl_static'
            ldLibs.addAll(['log'])
            cppFlags.addAll(['-std=c++11', '-Wall',
                             '-fno-exceptions', '-fno-rtti'])
            abiFilters.addAll(['armeabi', 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64', 'mips', 'mips64'])
        }

        sources {
            main {
                jni {
                    dependencies {
                        project ':lib-leveldb' linkage 'static'
                        project ':lib-snappy' linkage 'static'
                    }
                    source {
                        srcDirs 'src/main/jni'
                    }
                }
            }
        }
        buildTypes {
            release {
                minifyEnabled = false
                proguardFiles.add(file('proguard-rules.txt'))
            }
        }
    }

}

dependencies {
    androidTestCompile 'junit:junit-dep:4.11'
    androidTestCompile 'com.google.truth:truth:0.25'
    androidTestCompile 'com.android.support.test:runner:0.3'
    androidTestCompile 'com.android.support.test:rules:0.3'
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name "LevelDB-jni"

                description appDescription

                url appWebsiteUrl

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'jacek'
                        name 'Jacek Marchwicki'
                        email 'jacek.marchwicki@gmail.com'
                    }
                }
                scm {
                    connection 'scm:git:https://github.com/jacek-marchwicki/leveldb-jni.git'
                    developerConnection 'scm:git:https://github.com/jacek-marchwicki/leveldb-jni.git'
                    url appGirUrl
                }
            }
        }
    }
}

apply plugin: 'com.jfrog.bintray'

artifacts {
}

bintray {
    user = project.ext.has("bintray.user") ? project.ext.get("bintray.user") : ""
    key = project.ext.has("bintray.apikey") ?  project.ext.get("bintray.apikey") : ""

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = archivesBaseName
        desc = appDescription
        licenses = ["Apache-2.0"]
        websiteUrl = appWebsiteUrl
        vcsUrl = appGirUrl
        publish = true
        publicDownloadNumbers = true
        version {
            gpg {
                sign = true
            }
        }
    }
}